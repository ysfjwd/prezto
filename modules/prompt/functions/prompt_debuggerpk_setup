#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Yousuf Jawwad <yousuf@debugger.pk>
#
# Screenshots:
#   http://i.imgur.com/gLgVp6Y.png
#

pmodload 'helper'

function prompt_debuggerpk_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_debuggerpk_pwd="$MATCH"
    unset MATCH
  else
    _prompt_debuggerpk_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_debuggerpk_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

    # Get Python information.
  if (( $+functions[python-info] )); then
    python-info
  fi

  #Sets the prompt
  prompt_debuggerpk_pwd
}

function prompt_debuggerpk_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_debuggerpk_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _clrs=(
      "%F{81}"  # Turquoise
      "%F{226}" # Orange
      "%F{135}" # Purple
      "%F{200}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _clrs=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_clrs[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format " ${_clrs[5]}✚%f"
  zstyle ':prezto:module:git:info:deleted' format " ${_clrs[2]}✖%f"
  zstyle ':prezto:module:git:info:modified' format " ${_clrs[4]}✱%f"
  zstyle ':prezto:module:git:info:untracked' format " ${_clrs[3]}☉%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b%d%a%m%u'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format 'ruby/[%v]'

  # Set Python Info Parameters
  zstyle ':prezto:module:python:info:virtualenv' format '❝ py/[%v] ❞ '

  # Define prompts.
  #PROMPT="${_clrs[3]}%n%f at ${_clrs[2]}%m%f"'%F${_clrs[4]} $python_info[virtualenv]%f'"in ${_clrs[5]}%~%f ❯ "
  PROMPT="${_clrs[3]}%n%f at ${_clrs[2]}%m%f"'%F${_clrs[4]} $python_info[virtualenv]%f'"in "'%F${_clrs[5]}${_prompt_debuggerpk_pwd}%f'"%f ❯ "
  RPROMPT='${git_info:+${(e)git_info[prompt]}}'
}

prompt_debuggerpk_setup "$@"
